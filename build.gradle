apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.feedhenry.gitlabshell'
archivesBaseName = 'gitlab-shell-client'
sourceCompatibility = 1.6
version = '4.0.1'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "net.saliman:gradle-cobertura-plugin:2.2.4"
    }
}
apply plugin: 'cobertura'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gitlab Shell Client', 'Implementation-Version': version
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.jcraft:jsch:0.1.51'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    testCompile 'junit:junit:[4,5)'
    testCompile 'org.mockito:mockito-all:1.9.5'
}

signing {
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Gitlab Shell Client'
        packaging 'jar'
        // optionally artifactId can be defined here
        description 'Java Client for remote administration of Gitlab Shell'
        url 'https://github.com/feedhenry/gitlab-shell-client'

        scm {
          connection 'scm:git:https://github.com/feedhenry/gitlab-shell-client.git'
          developerConnection 'scm:git:https://github.com/feedhenry/gitlab-shell-client.git'
          url 'https://github.com/feedhenry/gitlab-shell-client.git'
        }

        licenses {
          license {
            name 'The MIT License (MIT)'
            url 'http://opensource.org/licenses/MIT'
          }
        }

        developers {
          developer {
            id 'david.martin.fh'
            name 'David Martin'
            email 'david.martin@feedhenry.com'
          }
        }
      }
    }
  }
}
